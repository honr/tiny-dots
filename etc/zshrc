#!/usr/bin/zsh

HISTFILE=~/.history/zsh
HISTSIZE=32768
SAVEHIST=65536
WORDCHARS=''
bindkey -e
bindkey '^W'  kill-region
bindkey '^[n' history-beginning-search-forward
bindkey '^[p' history-beginning-search-backward

# From bashrc
# shopt -s histappend
# shopt -s checkwinsize
# set +o histexpand
# [[ -t 0 && -t 1 ]] && stty werase ^-
# TERM="xterm-256color"

zstyle :compinstall filename "$HOME/.local/zsh/rc"
autoload -Uz compinit ; compinit

setopt prompt_subst
autoload -Uz vcs_info
zstyle ':vcs_info:*' actionformats \
       '[%r %b]' 'branch="%b" repo="%r" subdir="%S" action="%a"'
zstyle ':vcs_info:*' formats \
       '[%r %b]' 'branch="%b" repo="%r" subdir="%S"'
zstyle ':vcs_info:*' enable git
precmd() {
  vcs_info
  print -Pn "\e]0;%n@%m:${vcs_info_msg_0_}%~ %D{w%V %a %H:%M:%S}\a"
}

PROMPT='%F{142}%B%i. %n@%m:%F{149}${vcs_info_msg_0_}%F{75}%1~%f %B%F{144}%D{%a %H:%M %s} %(?:%b%F{35}:%B%F{red})><(((ยบ>%b
%B%F{142}$%f%b '
PROMPT2='  '
# RPROMPT='%F{yellow}${vcs_info_msg_1_}%b'

# Experimental; haven't decided to use plugin.
autoload -Uz predict-on
zle -N predict-on
zle -N predict-off
bindkey '^X^Z' predict-on
bindkey '^Z' predict-off

export VISUAL="emacsclient"
export EDITOR="emacsclient"
export ALTERNATE_EDITOR=""
# export CLICOLOR=1 # Same as ls -G

source "$HOME/Pool/etc/zshrc.$UNAME" 2>/dev/null
source "$HOME/Pool-site/etc/zshrc-site" 2>/dev/null
source "$HOME/Pool-host/etc/zshrc-host" 2>/dev/null

bell() { echo -e '\a' }

la() { ls -Glarth }

e.pwd() {
  pwd
  if [[ -n "${vcs_info_msg_1_}" ]] ; then
      echo -e "vcs info: ${vcs_info_msg_1_}"
  fi
}

clj() {
  if [[ "$1" == "-i" ]] ; then
      shift
      clove -i clojure "$@"
  else
    clove -c clojure "$@"
  fi
}

sx() {
  if [[ -z "$1" ]] ; then
    screen -ls
  else
    STY="$1" screen -xR "$1"
  fi
}

ee() {
  if [[ -z "$1" ]] ; then
      emacsclient -nc .
  else
    emacsclient -nc "$@"
  fi
}

# `echo' with color codes interleaved. Example:
# cecho @521 Hello @20/04 World
function cecho() {
  local reset_color='\033[0m'
  local arg color
  for arg; do
    if [[ "$arg" =~ ^@[0-9]{2,3}$ ]] ; then
        local fg="${arg/@/}"
        if [[ ${#fg} == 2 ]] ; then
            fg="$(( 10#$fg + 232 ))"
        else
          fg="$(( 6#$fg + 16 ))"
        fi
        color="\\033[38;5;${fg}m"
    elif [[ "$arg" =~ ^@[0-9]{2,3}[/][0-9]{2,3}$ ]] ; then
        arg="${arg/@/}"
        local fg="${arg%%/*}"
        if [[ ${#fg} == 2 ]] ; then
            fg="$(( 10#$fg + 232 ))"
        else
          fg="$(( 6#$fg + 16 ))"
        fi
        local bg="${arg##*/}"
        if [[ ${#bg} == 2 ]] ; then
            bg="$(( 10#$bg + 232 ))"
        else
          bg="$(( 6#$bg + 16 ))"
        fi
        color="\\033[38;5;${fg}m\\033[48;5;${bg}m"
    elif [[ "$arg" =~ ^@[0-9]$ ]] ; then
        local fg="${arg/@/}"
        color="\\033[3${fg}m"
    elif [[ "$arg" == "@" ]] ; then
        color="$reset_color"
    else # Not a color
      if [[ -n "$color" ]] ; then
          echo -ne "$color"
      fi
      echo -n "$arg"
      if [[ -n "$color" ]] ; then
          echo -ne "$reset_color"
      fi
      color=""
    fi
  done
  echo
}
