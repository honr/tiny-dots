#!/bin/zsh

HISTFILE=~/.history/zsh
HISTSIZE=32768
SAVEHIST=65536
WORDCHARS=''
bindkey -e
bindkey '^W'  kill-region
bindkey '^[n' history-beginning-search-forward
bindkey '^[p' history-beginning-search-backward

setopt SHARE_HISTORY

# From bashrc
# shopt -s histappend
# shopt -s checkwinsize
# set +o histexpand
# [[ -t 0 && -t 1 ]] && stty werase ^-
# TERM="xterm-256color"

zstyle :compinstall filename "$HOME/.zshrc"
autoload -Uz compinit ; compinit

setopt prompt_subst
autoload -Uz vcs_info
zstyle ':vcs_info:*' actionformats \
       '[%r %b]' 'branch="%b" repo="%r" subdir="%S" action="%a"'
zstyle ':vcs_info:*' formats \
       '[%r %b]' 'branch="%b" repo="%r" subdir="%S"'
zstyle ':vcs_info:*' enable git

precmd() {
  vcs_info
  print -Pn "\e]0;%n@%m:${vcs_info_msg_0_}%~ %D{w%V %a %H:%M:%S}\a"
}

PROMPT='%F{142}%B%i. %n@%m:%F{149}${vcs_info_msg_0_}%F{75}%1~%f %B%F{144}%D{%a %H:%M %s} %(?:%b%F{35}><(((°>:%B%F{red}><(((ˣ>)%b
%B%F{142}$%f%b '
PROMPT2='  '
# RPROMPT='%F{yellow}${vcs_info_msg_1_}%b'

# Experimental; haven't decided to use plugin.
autoload -Uz predict-on
zle -N predict-on
zle -N predict-off
bindkey '^X^Z' predict-on
bindkey '^Z' predict-off

export VISUAL="emacsclient"
export EDITOR="emacsclient"
export ALTERNATE_EDITOR=""
# export CLICOLOR=1 # Same as ls -G or ls --color=auto

source "$HOME/Pool/etc/zshrc.$UNAME" 2>/dev/null
source "$HOME/Pool-site/etc/zshrc-site" 2>/dev/null
source "$HOME/Pool-host/etc/zshrc-host" 2>/dev/null

bell() { echo -e '\a' }

la() { ls -larthF }

e.pwd() {
  pwd
  if [[ -n "${vcs_info_msg_1_}" ]] ; then
      echo -e "vcs info: ${vcs_info_msg_1_}"
  fi
}

clj() {
  if [[ "$1" == "-i" ]] ; then
      shift
      clove -i clojure "$@"
  else
    clove -c clojure "$@"
  fi
}

sx() {
  if [[ -z "$1" ]] ; then
    screen -ls
  else
    STY="$1" screen -xR "$1"
  fi
}

ee() {
  if [[ -z "$1" ]] ; then
      emacsclient -nc .
  elif [[ "$1" =~ ^- ]] ; then
      emacsclient "$@"
  else
    emacsclient -nc "$@"
  fi
}

# `echo' with color codes, and with neither escaping (-e) nor (-n). Example:
# cecho -521: Hello -20:4 World -
cecho() {
  local arg
  for arg; do
    if [[ "$arg" == "-" ]] ; then # Reset colors.
        echo -ne '\033[0m'
    elif [[ "$arg" =~ ^-[0-9]{0,3}:[0-9]{0,3}$ ]] ; then
        arg="${arg/-/}"
        __cecho_helper "\\033[38;5;" "${arg%%:*}"
        __cecho_helper "\\033[48;5;" "${arg##*:}"
    else
      echo -n "$arg"
    fi
  done
  echo
}
__cecho_helper() {
  local prefix="$1"
  local color="$2"
  if [[ -z "$color" ]]; then return; fi
  if [[ "${#color}" == 3 ]] ; then
      echo -ne "${prefix}$(( 6#$color + 16 ))m"
  else
    echo -ne "${prefix}$(( 10#$color + 232 ))m"
  fi
}
