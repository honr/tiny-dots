#!/bin/zsh

# zshenv: all types
# zprofile, zlogin, zlogout: login shells
# zshrc: interactive shells
# The order is: zshenv, zprofile, zshrc, zlogin, run, zlogout

HISTFILE=~/.history/zsh
HISTSIZE=32768
SAVEHIST=65536
WORDCHARS=''
bindkey -e
bindkey '^W'  kill-region
bindkey '^[n' history-beginning-search-forward
bindkey '^[p' history-beginning-search-backward

# setopt SHARE_HISTORY
# setopt INC_APPEND_HISTORY
setopt INC_APPEND_HISTORY_TIME

# From bashrc
# shopt -s histappend
# shopt -s checkwinsize
# set +o histexpand
# [[ -t 0 && -t 1 ]] && stty werase ^-
# TERM="xterm-256color"

fpath=($fpath $HOME/Pool/base/zsh)
zstyle :compinstall filename "$HOME/.zshrc"
autoload -Uz compinit ; compinit

setopt prompt_subst
autoload -Uz vcs_info
zstyle ':vcs_info:*' max-exports 4
zstyle ':vcs_info:*' actionformats \
       '[%r %b]' 'branch="%b" repo="%r" subdir="%S" action="%a"' '%R' '%r'
zstyle ':vcs_info:*' formats \
       '[%r %b]' 'branch="%b" repo="%r" subdir="%S"' '%R' '%r'
zstyle ':vcs_info:*' enable git

precmd() {
  vcs_info
  print -Pn "\e]0;%n@%m:${vcs_info_msg_0_}%~ %D{w%V %a %H:%M:%S}\a"
}

PROMPT='%F{142}%B%i. %n@%m:%F{149}${vcs_info_msg_0_}%F{75}%1~%f '
PROMPT+='%B%F{144}%D{%a %H:%M %s} '
PROMPT+='%(?:%b%F{35}><(((Â°>:%B%F{red}><(((">)%b '
PROMPT+=$'\n'
PROMPT+='%B%F{142}$%f%b '
PROMPT2='  '
# RPROMPT='%F{yellow}${vcs_info_msg_1_}%b'

# Experimental; haven't decided to use plugin.
autoload -Uz predict-on
zle -N predict-on
zle -N predict-off
bindkey '^X^Z' predict-on
bindkey '^Z' predict-off

export VISUAL="emacsclient"
export EDITOR="emacsclient"
export ALTERNATE_EDITOR=""
# export CLICOLOR=1 # Same as ls -G or ls --color=auto

bell() { echo -e '\a' }

la() { ls -larthF }

e.pwd() {
  pwd
  if [[ -n "${vcs_info_msg_1_}" ]] ; then
      echo -e "vcs info: ${vcs_info_msg_1_}"
  fi
}

clj() {
  if [[ "$1" == "-c" ]] ; then
      shift
      clove -c clojure "$@"
  else
    clove -i clojure "$@"
  fi
}

ee() {
  if [[ "$1" =~ ^- ]] ; then
      emacsclient "$@"
  elif [[ -z "$DISPLAY" ]] ; then
    emacsclient -t "$@"
  else
    emacsclient -nc "$@"
  fi
}

edit-tt() {
  local ws="$1"
  if [[ -z "$ws" ]] ; then
    ws="$STY"
  fi
  if [[ -z "$ws" ]] ; then
    echo "A ws name should be specified."
    echo 1
  fi
  ee "$HOME/.tmux/$ws"
}

br() {
  if [[ -z "$1" ]] ; then
    git branch
  else
    git checkout "$@"
  fi
}

randword() {
  local wordsfile="/usr/share/dict/words"
  local size="$(wc -l < $wordsfile)"
  local rand1="$RANDOM"
  local rand2="$RANDOM"
  head -n "$(( ($rand1 << 15 | $rand2) % $size + 1))" "$wordsfile" | tail -n 1
}

source "$HOME/Pool/base/etc/zshrc.$UNAME" 2>/dev/null
source "$HOME/Pool/site/etc/zshrc" 2>/dev/null
source "$HOME/Pool/host/etc/zshrc" 2>/dev/null
